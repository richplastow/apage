Class Article
=============

#### Represents a single article within the page




Define the `Article` class
--------------------------

    class Article
      I: 'Article'
      toString: -> "[object #{@I}]"




Define private class properties
-------------------------------

By convention, private properties are prefixed with an underscore. Developers 
should avoid getting or setting these directly. 

#### `_rules`
See `ªpopulate()` for a description of the `rule` format. 

      _rules:
        config: [
          ['path',undefined,'string',/^[a-z0-9][-\/a-z0-9]{0,63}\.[.a-z0-9]+$/i] #@todo looser allowed characters
          ['raw' ,''       ,'string',/^[^\x00-\x08\x0E-\x1F]{0,10000}$/] #@todo better list of valid characters
        ]
        properties: [
          ['id'   ,undefined,'string',/^apage_[-_0-9a-z]{1,10}$/]
          ['order',undefined,'string',/^[-_0-9a-z]{1,10}$/]
        ]




Define private instance properties
----------------------------------

      constructor: (config={}) ->

#### `_config`
The instance configuration. Use `config()` to get and set it. 

        @_config = {}




Constructor functionality
-------------------------

Validate `config` against `_rules.config`, and populate `_config` if it passes. 

        if errors = ªpopulate config, @_config, @_rules.config
          throw new Error 'Invalid `config`:\n  ' + errors.join '\n  '




Define public properties
------------------------

#### `path`
This will usually be a relative path, eg 'doc/03-third-doc-article.md'. Note 
that leading and trailing dots and slashes are removed. 

        @path = @_config.path.replace /^[.\/]+|[.\/]+$/g, ''




#### `id`
Derived from `path`, eg '3-Top Cafés/7-Bob’s.md' has id 'apage_top-cafes_bobs'. 

        @id = ( parseFilename fname, 'slug' for fname,i in @path.split '/' )
        @id = 'apage_' + @id.join '_' # prefix prevents numeric-name confusion




#### `order`
Also derived from `path`, eg '3-Top Cafés/7-Bob’s.md' has order string '7'. 

        @order = parseFilename fname, 'order'




#### `front`
frontmatter is optional, based on [Jeyll frontmatter](http://goo.gl/4l6k2e). @todo validate Jeyll frontmatter style

        @front = [] # will remain an empty array if no frontmatter is present

Determine whether `raw` contains frontmatter. If so, process each of its lines.  

        if '---\n' == @_config.raw.substr 0, 4 # the raw string’s first 4 chars
          @_config.raw = @_config.raw.split '---\n' # three or more sections
          for line,i in @_config.raw[1].split '\n' # look in the second section

Get each key/value pair. @todo trim comments http://jekyllrb.com/docs/configuration/#front-matter-defaults

            [key, value...] = line.split ':'
            key   = key?.replace   /^\s+|\s+$/g, ''
            value = value?.join(':').replace /^\s+|\s+$/g, ''
            if ! key or ! value then continue

The `id`, `order`, and `title` fields override autogenerated values. All other 
fields are recorded in the `front` array. 

            switch key
              when 'id'
                @id = "apage_#{value.replace /^apage_/, ''}" # enforced prefix
              when 'order'
                @order = if isNaN value*1 then value else parseInt value, 10
              when 'title'
                @title = value
              else
                @front.push [ key, value ]

Remove the frontmatter section from the raw markdown, and validate `front`. 

          @_config.raw = (@_config.raw.slice 2).join '---\n'
          #@todo validate `front`, as well as `id`, `order`, and `title`

Trim newlines and extra spaces from the start and end of the raw markdown. 

        @_config.raw = @_config.raw.replace /^\s+|\s+$/g, ''




#### `title`
Unless set in frontmatter, an article’s `title` is its first line of markdown. 

        @title = @title || (@_config.raw.split '\n')[0]




#### `html`
The article’s HTML is parsed from the raw markdown using the `marked` library. 

        @html = (marked @_config.raw).replace(/\\/g, '\\\\').split '\n'




#### Config can override properties
The properties defined in `_rules.properties` can be overridden by `confg`. 

        if errors = ªpopulate config, @, @_rules.properties, yes #@todo test the `updating` arg
          throw new Error 'Invalid `config`:\n  ' + errors.join '\n  '




Define public methods
---------------------

#### `clone()`
Returns a copy of the article instance. This is used by `apage.read()`, because 
returning an actual reference to the article would allow the caller to modify 
it in unexpected ways. 

      clone: ->
        ªclone @, ['id', 'path', 'order']




#### `destructor()`
Xx. @todo description

      destructor: -> #@todo




#### `edit()`
Modifies instance properties. After the update is complete, `edit()` usually 
returns `undefined`. However, an attempt to edit properties to an invalid state 
will fail, and return an array of error messages. 

      edit: (amend) -> #@todo 
        ªpopulate amend, @, @_rules.properties, yes #@todo test the `updating` arg




#### `config()`
Gets or sets the current configuration. When setting configuration, `config()` 
usually returns `undefined`. However, an attempt to set the configuration to 
an invalid state will fail, and return an array of error messages. 

      config: (key, value) ->
        switch arguments.length

- `config()` returns a clone of the current configuration, as an object

          when 0
            ªclone @_config, @_rules.config

          when 1
            switch ªtype key

- `config('recognized')` returns the current value of `recognized`
- `config('X')` returns `undefined`, assuming 'X' is not a recognized key

              when ªS
                @_config[key]

- `config({a:1, b:1})` sets `a` and `b` to new values, and returns `undefined`
- `config({a:'X', b:'X'})` leaves `a` and `b` alone, returning two errors
- `config({a:1, b:'X'})` leaves `a` and `b` alone, returning one error

Note this last example: the value of `a` is valid, but because `b`’s value is 
invalid the whole operation fails, so `a`’s value remains unchanged. 

              when ªO
                ªpopulate key, @_config, @_rules.config, yes #@todo test the `updating` arg

- `config('foo', 'ok value')` sets `foo` to 'ok value', and returns `undefined`
- `config('bar', 'X')` leaves `bar` alone, and returns an error-message array

          when 2
            obj = {}
            obj[key] = value
            @config obj



